; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "NAnt-Gui"
#define MyAppPublisher "Colin Svingen"
#define MyAppURL "nantgui.berlios.de"
#define MyAppExeName "NAnt-Gui.exe"

#define NAnt = "Tools\nant-0.91-alpha2"
#define NAntContrib = "Tools\nantcontrib-0.85"

#define AppVersion = GetFileVersion(AddBackslash(SourcePath) + "NAnt-Gui\bin\Release\NAnt-Gui.exe")


[Setup]
AppName={#MyAppName}
AppVerName={#MyAppName} {#AppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=true
OutputDir=Installer
OutputBaseFilename={#MyAppName}-{#AppVersion}
Compression=lzma
SolidCompression=true
ShowLanguageDialog=yes
ChangesAssociations=true
LicenseFile=NAnt-Gui.Docs\License.txt
AppCopyright=2005-2010 Colin Svingen
AppVersion={#AppVersion}
AppID={{5A46EB86-CC66-403A-9789-E7D7413C20D2}
AppContact=swoogan@gmail.com
UninstallDisplayIcon={app}\src\Ant.ico
VersionInfoVersion={#AppVersion}
VersionInfoCopyright=2005-2010 Colin Svingen
VersionInfoProductName={#MyAppName}

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: assocbuild; GroupDescription: Associate with file types; Description: Associate with .build files
Name: assocnant; GroupDescription: Associate with file types; Description: Associate with .nant files; Flags: unchecked
Name: envpath; GroupDescription: Set environment variables; Description: Add bin directory to PATH

[Files]
; ### Binary ###
; Documents
Source: NAnt-Gui.Docs\*.txt; DestDir: {app}; Flags: ignoreversion; Excludes: Readme.Txt
Source: NAnt-Gui.Docs\Readme.Txt; DestDir: {app}; Flags: ignoreversion isreadme
Source: NAnt-Gui.Docs\BlankProject.build; DestDir: {app}\Data; Flags: ignoreversion
Source: Schemas\*; DestDir: {app}\Data\Schemas; Flags: ignoreversion recursesubdirs
; NAnt-Gui
Source: NAnt-Gui\bin\Release\NAnt-Gui.exe; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
Source: NAnt-Gui\bin\Release\NAnt-Gui.exe.config; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
Source: NAnt-Gui.Core\bin\Release\NAnt-Gui.Core.dll; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
Source: NAnt-Gui.Gui\bin\Release\NAnt-Gui.Gui.dll; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
Source: NAnt-Gui.Framework\bin\Release\NAnt-Gui.Framework.dll; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
Source: NAnt-Gui.NAnt\bin\Release\NAnt-Gui.NAnt.dll; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
Source: NAnt-Gui.MSBuild\bin\Release\NAnt-Gui.MSBuild.dll; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
; XmlEditor
Source: XmlEditor\Project\bin\Release\XmlEditor.dll; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
; Libraries
Source: ThirdParty Libraries\*; DestDir: {app}\bin; Flags: ignoreversion; Components: bin
; NAnt/NAnt-Contrib
Source: {#NAnt}\bin\*; DestDir: {app}\bin; Flags: ignoreversion recursesubdirs; Components: bin\nant
Source: {#NAnt}\examples\*; DestDir: {app}\examples; Flags: ignoreversion recursesubdirs; Components: examples
Source: {#NAnt}\doc\*; DestDir: {app}\nant-docs; Flags: ignoreversion recursesubdirs; Components: docs
Source: {#NAntContrib}\bin\*; DestDir: {app}\bin; Flags: ignoreversion recursesubdirs; Components: bin\contrib
Source: {#NAntContrib}\doc\*; DestDir: {app}\nantcontrib-docs; Flags: ignoreversion recursesubdirs; Components: docs
; InnoSetupTask
Source: Nant.InnoSetup.Tasks\bin\Release\NAnt.InnoSetup.Tasks.dll; DestDir: {app}\bin; Flags: ignoreversion; Components: bin\innosetup

; ### Source ###
Source: NAnt-Gui.sln; DestDir: {app}\src; Flags: ignoreversion; Components: src
Source: NAnt-Gui.build; DestDir: {app}\src; Flags: ignoreversion; Components: src
Source: NAnt-Gui.iss; DestDir: {app}\src; Flags: ignoreversion; Components: src
Source: build_number.txt; DestDir: {app}\src; Flags: ignoreversion; Components: src
Source: Ant.ico; DestDir: {app}\src; Flags: ignoreversion; Components: src
Source: build.bat; DestDir: {app}\src; Flags: ignoreversion; Components: src
; NAnt-Gui.Docs
Source: NAnt-Gui.Docs\*.*; DestDir: {app}\src\NAnt-Gui.Docs; Flags: ignoreversion; Components: src
; Libraries
Source: ThirdParty Libraries\*; DestDir: {app}\src\ThirdParty Libraries; Flags: ignoreversion; Components: src
; NAnt-Gui
Source: NAnt-Gui\*.config; DestDir: {app}\src\NAnt-Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui\*.cs; DestDir: {app}\src\NAnt-Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui\*.csproj; DestDir: {app}\src\NAnt-Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui\*.build; DestDir: {app}\src\NAnt-Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui\Ant.ico; DestDir: {app}\src\NAnt-Gui; Flags: ignoreversion; Components: src
; NAnt-Gui.Unittests
Source: NAnt-Gui.Unittests\*.cs; DestDir: {app}\src\NAnt-Gui.Unittests; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Unittests\*.resx; DestDir: {app}\src\NAnt-Gui.Unittests; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.Unittests\*.csproj; DestDir: {app}\src\NAnt-Gui.Unittests; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Unittests\*.build; DestDir: {app}\src\NAnt-Gui.Unittests; Flags: ignoreversion; Components: src
; NAnt-Gui.Gui
Source: NAnt-Gui.Gui\*.cs; DestDir: {app}\src\Nant-Gui.Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\*.resx; DestDir: {app}\src\Nant-Gui.Gui; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.Gui\*.csproj; DestDir: {app}\src\NAnt-Gui.Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\*.build; DestDir: {app}\src\NAnt-Gui.Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\*.config; DestDir: {app}\src\Nant-Gui.Gui; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\Controls\*.cs; DestDir: {app}\src\NAnt-Gui.Gui\Controls; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\Controls\*.resx; DestDir: {app}\src\NAnt-Gui.Gui\Controls; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\Properties\*.cs; DestDir: {app}\src\NAnt-Gui.Gui\Properties; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\Properties\*.settings; DestDir: {app}\src\NAnt-Gui.Gui\Properties; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Gui\Properties\*.resx; DestDir: {app}\src\NAnt-Gui.Gui\Properties; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.Gui\Images\*; DestDir: {app}\src\Nant-Gui.Gui\Images; Flags: ignoreversion; Components: src
; NAnt-Gui.Core
Source: NAnt-Gui.Core\*.cs; DestDir: {app}\src\NAnt-Gui.Core; Flags: ignoreversion recursesubdirs skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.Core\*.resx; DestDir: {app}\src\NAnt-Gui.Core; Flags: ignoreversion skipifsourcedoesntexist recursesubdirs; Components: src
Source: NAnt-Gui.Core\*.csproj; DestDir: {app}\src\NAnt-Gui.Core; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Core\*.build; DestDir: {app}\src\NAnt-Gui.Core; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Core\Properties\*.cs; DestDir: {app}\src\NAnt-Gui.Core\Properties; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Core\Properties\*.settings; DestDir: {app}\src\NAnt-Gui.Core\Properties; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.Core\Properties\*.resx; DestDir: {app}\src\NAnt-Gui.Core\Properties; Flags: ignoreversion; Components: src
; NAnt-Gui.Framework
Source: NAnt-Gui.Framework\*.cs; DestDir: {app}\src\NAnt-Gui.Framework; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Framework\*.resx; DestDir: {app}\src\NAnt-Gui.Framework; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.Framework\*.csproj; DestDir: {app}\src\NAnt-Gui.Framework; Flags: ignoreversion; Components: src
Source: NAnt-Gui.Framework\*.build; DestDir: {app}\src\NAnt-Gui.Framework; Flags: ignoreversion; Components: src
; NAnt-Gui.NAnt
Source: NAnt-Gui.NAnt\*.cs; DestDir: {app}\src\NAnt-Gui.NAnt; Flags: ignoreversion; Components: src
Source: NAnt-Gui.NAnt\*.resx; DestDir: {app}\src\NAnt-Gui.NAnt; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.NAnt\*.csproj; DestDir: {app}\src\NAnt-Gui.NAnt; Flags: ignoreversion; Components: src
Source: NAnt-Gui.NAnt\*.build; DestDir: {app}\src\NAnt-Gui.NAnt; Flags: ignoreversion; Components: src
; NAnt-Gui.MSBuild
Source: NAnt-Gui.MSBuild\*.cs; DestDir: {app}\src\NAnt-Gui.MSBuild; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.MSBuild\*.resx; DestDir: {app}\src\NAnt-Gui.MSBuild; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt-Gui.MSBuild\*.csproj; DestDir: {app}\src\NAnt-Gui.MSBuild; Flags: ignoreversion; Components: src
Source: NAnt-Gui.MSBuild\*.build; DestDir: {app}\src\NAnt-Gui.MSBuild; Flags: ignoreversion; Components: src
; NAnt.InnoSetup.Tasks
Source: NAnt.InnoSetup.Tasks\*.cs; DestDir: {app}\src\NAnt.InnoSetup.Tasks; Flags: ignoreversion; Components: src
Source: NAnt.InnoSetup.Tasks\*.csproj; DestDir: {app}\src\NAnt.InnoSetup.Tasks; Flags: ignoreversion; Components: src
Source: NAnt.InnoSetup.Tasks\Sample.iss; DestDir: {app}\src\NAnt.InnoSetup.Tasks; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: NAnt.InnoSetup.Tasks\Properties\*.cs; DestDir: {app}\src\NAnt.InnoSetup.Tasks\Properties; Flags: ignoreversion; Components: src
; Schemas
Source: Schemas\*; DestDir: {app}\src\Schemas; Flags: ignoreversion recursesubdirs; Components: src
; Tools
Source: Tools\*; DestDir: {app}\src\Tools; Flags: ignoreversion recursesubdirs; Components: src
; XmlEditor
Source: XmlEditor\XmlEditor.sln; DestDir: {app}\src\XmlEditor; Flags: ignoreversion; Components: src
Source: XmlEditor\Doc\*.txt; DestDir: {app}\src\XmlEditor\Doc; Flags: ignoreversion skipifsourcedoesntexist; Components: src
Source: XmlEditor\Test\Completion\*.*; DestDir: {app}\src\XmlEditor\Test\Completion; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\FindSchemaObject\*.*; DestDir: {app}\src\XmlEditor\Test\FindSchemaObject; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Parser\*.*; DestDir: {app}\src\XmlEditor\Test\Parser; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Paths\*.*; DestDir: {app}\src\XmlEditor\Test\Paths; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Resources\*.*; DestDir: {app}\src\XmlEditor\Test\Resources; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Schema\*.*; DestDir: {app}\src\XmlEditor\Test\Schema; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Schema.Includes\*.*; DestDir: {app}\src\XmlEditor\Test\Schema.Includes; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Schema.Uri\*.*; DestDir: {app}\src\XmlEditor\Test\Schema.Uri; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Tree\*.*; DestDir: {app}\src\XmlEditor\Test\Tree; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\Utils\*.*; DestDir: {app}\src\XmlEditor\Test\Utils; Flags: ignoreversion; Components: src
Source: XmlEditor\Test\XPathQuery\*.*; DestDir: {app}\src\XmlEditor\Test\XPathQuery; Flags: ignoreversion; Components: src
Source: XmlEditor\Project\*.*; DestDir: {app}\src\XmlEditor\Project; Flags: ignoreversion; Components: src
Source: XmlEditor\Project\Configuration\*.*; DestDir: {app}\src\XmlEditor\Project\Configuration; Flags: ignoreversion; Components: src
Source: XmlEditor\Project\Resources\*.*; DestDir: {app}\src\XmlEditor\Project\Resources; Flags: ignoreversion; Components: src
Source: XmlEditor\Project\Src\*.*; DestDir: {app}\src\XmlEditor\Project\Src; Flags: ignoreversion; Components: src

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\bin\{#MyAppExeName}
Name: {userdesktop}\{#MyAppName}; Filename: {app}\bin\{#MyAppExeName}; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\bin\{#MyAppExeName}; Tasks: quicklaunchicon
Name: {group}\Documentation\NAnt-SDK Help; Filename: {app}\nant-docs\sdk\NAnt-SDK.chm; Components: docs
Name: {group}\Documentation\NAnt Documentation; Filename: {app}\nant-docs\help\index.html; Components: docs
Name: {group}\Documentation\NAnt Contrib Docs; Filename: {app}\nantcontrib-docs\help\index.html; Components: docs
Name: {group}\{cm:UninstallProgram, {#MyAppName}}; Filename: {uninstallexe}

[Run]
Filename: {app}\bin\{#MyAppExeName}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent

[Dirs]
;Name: {app}\src; Components: src
;Name: {app}\bin; Components: bin
Name: {app}\src\NAnt-Gui\Properties; Components: src; Tasks: 
Name: {app}\src\NAnt-Gui.Framework\Properties; Components: src; Tasks: 
Name: {app}\src\NAnt-Gui.MSBuild\Properties; Components: src; Tasks: 
Name: {app}\src\NAnt-Gui.NAnt\Properties; Components: src; Tasks: 
Name: {app}\src\NAnt-Gui.Unittests\Properties; Components: src; Tasks: 

[Registry]
Root: HKCR; Subkey: .build; ValueType: string; ValueName: ; ValueData: NAntBuildFile; Flags: uninsdeletekey; Tasks: assocbuild
Root: HKCR; Subkey: NAntBuildFile; ValueType: string; ValueName: ; ValueData: NAnt Build File; Flags: uninsdeletekey; Tasks: assocbuild
Root: HKCR; Subkey: NAntBuildFile\DefaultIcon; ValueType: string; ValueName: ; ValueData: {app}\bin\{#MyAppExeName},0; Flags: uninsdeletevalue; Tasks: assocbuild
Root: HKCR; Subkey: NAntBuildFile\shell\Open\command; ValueType: string; ValueData: """{app}\bin\{#MyAppExeName}"" -f:""%1"""; Flags: uninsdeletevalue; Tasks: assocbuild
Root: HKCR; Subkey: NAntBuildFile\shell\Edit\command; ValueType: string; ValueData: """{sys}\notepad.exe"" ""%1"""; Flags: uninsdeletevalue; Tasks: assocbuild

Root: HKCR; Subkey: .nant; ValueType: string; ValueName: ; ValueData: NAntNAntFile; Flags: uninsdeletekey; Tasks: assocnant
Root: HKCR; Subkey: NAntNAntFile; ValueType: string; ValueName: ; ValueData: NAnt Build File; Flags: uninsdeletekey; Tasks: assocnant
Root: HKCR; Subkey: NAntNAntFile\DefaultIcon; ValueType: string; ValueName: ; ValueData: {app}\bin\{#MyAppExeName},0; Flags: uninsdeletevalue; Tasks: assocnant
Root: HKCR; Subkey: NAntNAntFile\shell\Open\command; ValueType: string; ValueData: """{app}\bin\{#MyAppExeName}"" -f:""%1"""; Flags: uninsdeletevalue; Tasks: assocnant
Root: HKCR; Subkey: NAntNAntFile\shell\Edit\command; ValueType: string; ValueData: """{sys}\notepad.exe"" ""%1"""; Tasks: assocnant

Root: HKLM; Subkey: SYSTEM\CurrentControlSet\Control\Session Manager\Environment; ValueType: string; ValueName: Path; ValueData: "{code:GetEnvPath}{app}\bin;"; Tasks: envpath; Check: IfNotInPath

[Components]
Name: bin; Description: Executable files; Types: custom compact full
Name: bin\nant; Description: NAnt 0.91-alpha2; Types: custom compact full
Name: bin\contrib; Description: NAnt Contrib 0.85; Types: full custom compact
Name: bin\innosetup; Description: NAnt InnoSetup Task; Types: full
Name: examples; Description: Example NAnt build files; Types: full
Name: docs; Description: NAnt documentation files; Types: custom full
Name: src; Description: Source code files; Types: full

[InstallDelete]
Name: {app}\src\Tools\ispack-5.1.6.exe; Type: files; Components: src
Name: {app}\src\Tools\ispack-5.3.8.exe; Type: files; Components: src
Name: {app}\src\Tools\nant-0.85-rc3; Type: filesandordirs; Components: src
Name: {app}\src\Tools\nant-0.85; Type: filesandordirs; Components: src
Name: {app}\src\Tools\nantcontrib-0.85-rc3; Type: filesandordirs; Components: src

[Code]
const
	KEY = 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment';


function GetLastChar(Input: String) : String;
var
	Len: Integer;
begin
	Len := Length(Input);
	if Len > 0 then
		Result := Copy(Input, Len, 1)
	else
		Result := ''
end;

{ Adds a semicolon to the given string if one is NOT already there }
function AddSemicolon(Input: String) : String;
var
	LastChar: string;
begin
	LastChar := GetLastChar(Input);
	if LastChar <> ';' then
		Result := Input + ';'
	else
		Result := Input
end;

function GetEnvPath(Param: string) : string;
var
	Path: string;
begin
	if RegQueryStringValue(HKLM, KEY, 'Path', Path) then
		Result := AddSemicolon(Path)
	else
		Result := '';
end;

function IfNotInPath() : Boolean;
var
	EnvPath: string;
	Path: string;
begin
	RegQueryStringValue(HKLM, KEY, 'Path', EnvPath);
	Path := ExpandConstant('{app}') + '\bin';
	Result := Pos(Path, EnvPath) = 0;
end;


procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
	EnvPath, Path: string;
	Location: Integer;
	Answer: Boolean;
begin
  if CurUninstallStep = usUninstall then
  begin
	RegQueryStringValue(HKLM, KEY, 'Path', EnvPath)
	Path := ExpandConstant('{app}') + '\bin;'

	{ In case they took the last semicolon off }
	EnvPath := AddSemicolon(EnvPath)

    Location := Pos(Path, EnvPath)

	if Location <> 0 then
	begin
	  Delete(EnvPath, Location, Length(Path))
	  RegWriteStringValue(HKLM, KEY, 'Path', EnvPath)
	end;
  end;
end;

#ifdef Debug
  #expr SaveToFile(AddBackslash(SourcePath) + "Preprocessed.iss")
#endif
